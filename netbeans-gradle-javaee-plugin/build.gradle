import java.nio.file.*

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "cz.kubacki.gradle.plugins:gradle-nbm-plugin:1.12.0"
  }
}

apply plugin: 'java'
apply plugin: "cz.kubacki.nbm"


String netbeansVersion = 'RELEASE81'

repositories {
    mavenCentral()
    maven { url 'http://bits.netbeans.org/nexus/content/groups/netbeans' }
    maven { url 'http://bits.netbeans.org/maven2/' }
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    maven { url 'http://dl.bintray.com/kelemen/maven' }
}

version = '1.0.3'
nbm {
    moduleName = 'netbeans-gradle-javaee-plugin';
    
    // ? requires('netbeans-gradle-plugin');
}


ext.netBeansInstallDir = 'C:/Program Files/NetBeans 8.2'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile 'org.netbeans.api:org-netbeans-modules-projectapi:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-api-annotations-common:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-java-project:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-api-java-classpath:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-api-web-webmodule:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-j2ee-core:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-j2ee-dd:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-j2ee-metadata:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-modules:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-util:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-util-lookup:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-filesystems:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-loaders:' + netbeansVersion
    compile 'org.netbeans.api:org-openide-nodes:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-nbjunit:' + netbeansVersion
    compile 'org.netbeans.api:org-netbeans-modules-j2eeserver:' + netbeansVersion

    compile 'org.netbeans.modules:org-netbeans-modules-web-beans:' + netbeansVersion
    compile 'org.netbeans.modules:org-netbeans-modules-j2ee-persistenceapi:' + netbeansVersion
    compile 'org.netbeans.modules:org-netbeans-modules-projectapi-nb:' + netbeansVersion
    
    compile 'com.github.kelemen:netbeans-gradle-plugin:1.3.9.1'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

task netBeansRun(type: Exec) {
    doFirst {
        def confFile = project.file("$project.buildDir/testuserdir/etc/netbeans.conf")
        confFile.parentFile.mkdirs()
        confFile.write "netbeans_extraclusters=\"$project.buildDir/module\""
    }

    workingDir "${project.buildDir}"

    def executable = System.getProperty('os.name').toLowerCase().contains('windows') ? 'netbeans.exe' : 'netbeans'
    if (project.hasProperty('netBeansInstallDir')) {
        executable = netBeansInstallDir + '/bin/' + executable
    } else {
        logger.warn('netBeansInstallDir property is not specified. Assuming \'netbeans\' executable is on PATH.')
    }
    commandLine executable, '--userdir', "$project.buildDir/testuserdir"
}


